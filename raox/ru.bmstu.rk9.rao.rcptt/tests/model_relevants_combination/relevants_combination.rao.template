enum Тип_обработки {токарная, фрезерная}
enum Состояние_станка {свободен, занят}
enum Состояние_детали {ожидает_обработки, обрабатывается, обработана}

type Станок {
	Тип_обработки тип_обработки
	Состояние_станка состояние
}

type Деталь {
	Тип_обработки тип_обработки
	Состояние_детали состояние
	double время_обработки
}

resource деталь_1 = Деталь.create(Тип_обработки.токарная, Состояние_детали.ожидает_обработки, 10)
resource деталь_2 = Деталь.create(Тип_обработки.токарная, Состояние_детали.ожидает_обработки, 20)
resource деталь_3 = Деталь.create(Тип_обработки.фрезерная, Состояние_детали.ожидает_обработки, 40)

resource станок_1 = Станок.create(Тип_обработки.токарная, Состояние_станка.свободен)
resource станок_2 = Станок.create(Тип_обработки.фрезерная, Состояние_станка.свободен)

operation Обработка() {
	relevants деталь, станок = combination(
		Деталь.all.filter[состояние.equals(Состояние_детали.ожидает_обработки)],
		Станок.all.filter[состояние.equals(Состояние_станка.свободен)
	]).filter[деталь.тип_обработки.equals(станок.тип_обработки)].minBySafe[деталь.время_обработки] of types Деталь, Станок

	def duration() {
		return деталь.время_обработки
	}

	def begin() {
		деталь.состояние = Состояние_детали.обрабатывается
		станок.состояние = Состояние_станка.занят
	}

	def end() {
		деталь.состояние = Состояние_детали.обработана
		станок.состояние = Состояние_станка.свободен
	}
}

def terminateCondition() {
	return Деталь.all.filter[!состояние.equals(Состояние_детали.обработана)].empty
}

logic L {
	activity a = new Activity(Обработка.create())
}
